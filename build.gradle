plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
}

def NAME = 'gopizza'
def VERSION = '0.0.1'

def BASE_GROUP = 'com.gopizza'
def JAVA_VERSION = 21

//config todos los proyectos
allprojects {
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'org.springframework.boot'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(JAVA_VERSION)
		}
	}

	repositories {
		mavenCentral()
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-amqp'
		implementation 'org.springframework.kafka:spring-kafka'
		implementation 'com.fasterxml.jackson.module:jackson-module-parameter-names'
		implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
		implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
        implementation 'com.aventrix.jnanoid:jnanoid:2.0.0'
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'org.springframework.kafka:spring-kafka-test'

		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}

	test {
		useJUnitPlatform()
		testLogging {
			events "passed", "skipped", "failed"
		}
	}
}

//config subproyectos (microservicios)
subprojects {
	group = "${BASE_GROUP}.${rootProject.name}"

	sourceSets {
		main {
			java { srcDirs = ['main'] }
			resources { srcDirs = ['main/resources'] }
		}

		test {
			java { srcDirs = ['test'] } 
			resources { srcDirs = ['test/resources'] }
		}
	}

	dependencies {
		testImplementation rootProject.sourceSets.main.output
		
        if (project.name != "shared") {
			implementation project(":shared")
			testImplementation project(":shared").sourceSets.test.output
		}
	}

	bootJar {
		enabled = false
	}

	jar {
		enabled = true
	}
}

//config proyecto principal apps
sourceSets {
	main {
		java { srcDirs = ['apps/main'] }
		resources { srcDirs = ['apps/main/resources'] }
	}

	test {
		java { srcDirs = ['apps/test'] }
		resources { srcDirs = ['apps/test/resources'] }
	}
}

bootJar {
	archiveBaseName.set(NAME)
	archiveVersion.set(VERSION)
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {
	implementation project(":ordering")
	implementation project(":production")
	implementation project(":shared")

	implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14")
    implementation 'com.vaadin:vaadin-spring-boot-starter:24.4.6'

	testImplementation project(":shared").sourceSets.test.output
}